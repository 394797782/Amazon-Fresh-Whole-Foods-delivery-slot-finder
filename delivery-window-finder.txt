-- CREDITS:
-- counting occurences of substring "Not available"
-- 		Nigel Garvey's post on https://macscripter.net/viewtopic.php?id=8571

-- used to click the continue button on the "Out of stock" page that appears periodically
-- source: http://www.cubemg.com/how-to-click-a-button-on-a-web-page-with-applescript/
to clickID(theId) --creates a function that we can use over and over again instead of writing this code over and over again
	
	tell application "Safari" -- lets AppleScript know what program to controll
		
		do JavaScript "document.getElementById('" & theId & "').click();" in document 1 -- performs JavaScript code that clicks on the element of a specific id
		
	end tell -- tells Applescript you are done talking to Safari
	
end clickID -- lets AppleScript know we are done with the function

-- variable definitions
set found_slot to false
set oos_keyword to "We're sorry we are unable to fulfill your entire order"
set oos_msg to "click 'continue' on out of stock page before closing this dialog box"
set unknown_page_msg to "Unknown amazon page was loaded. try to manually navigate back to the 'Schedule your order page', and then run the program again"
set slot_site_url to "https://www.amazon.com/gp/buy/shipoptionselect/handlers/display.html?hasWorkingJavascript=1"
set continue_button_id to "ufss-page-conductor-form"
set slot_page_keyword to "Schedule your order"
set no_slot_keyword to "Not available"

set is_first_run to true


-- create new empty window, with one empty tab
tell application "Safari"
	make new document
	set amzn_win_id to id of front window
	-- instead of creating a new tab in your current window, this creates a window and 'hides it by minimizing it. apple script doesn't allow you to move tabs around, all new tabs are created. an alternate solution would be to get the unique id of the tab and access it that way instead of putting the tab in a new window
	set amzn_win_id to id of front window
	
end tell

-- save the original text delimited to restore it at the end
set astid to AppleScript's text item delimiters

repeat while found_slot is false
	
	-- load the delivery slot page
	tell application "Safari"
		-- opens in a new tab every time instead of just using open url request, which would prompt "Are you sure you want to send a form again?" and prevent this from running neatly in the background
		tell window id amzn_win_id
			make new tab with properties {URL:slot_site_url}
		end tell
		if is_first_run is true then
			-- minimizes window on the first iteration so it can run quietly in background
			set miniaturized of window id amzn_win_id to true
			set is_first_run to false
		end if
		
		delay 20
		set siteText to (text of last tab of window id amzn_win_id) as string
	end tell
	
	
	set AppleScript's text item delimiters to no_slot_keyword
	set subStringCount to (count siteText's text items)
	
	-- PROCESS PAGE CONTENTS:
	
	-- Both days have no delivery slots available
	if (count siteText's text items) > 2 then
		
		
		-- closes the tab since no slot was found
		tell application "Safari"
			close (last tab of window id amzn_win_id)
		end tell
		
		log "no slots found"
		
		-- delay so you don't spam Amazon's site
		delay 10
	else
		-- < 2 instances of the no slot keyword found means a slot is free or we landed on a different page
		
		
		if siteText contains oos_keyword then
			-- landed on out of stock page
			say "Item out of stock. See pop up"
			
			-- click continue button to dismiss out of stock warning
			-- NOTE: currently doesn't work
			-- clickID(continue_button_id)
			
			display dialog oos_msg
			
		else if siteText contains slot_page_keyword then
			-- landed on delivery slot page
			display notification "Found delivery slot!" with title "Amazon" sound name "Sosumi"
			say "Success: Delivery slot found"
			set found_slot to true
			
			-- tab is left open and activated
			tell application "Safari"
				activate (last tab of window id amzn_win_id)
			end tell
			-- plays celebrate song - REMOVED for github
			-- do shell script "afplay -t 30 " & "/Users/adrianhertel/Downloads/song.php.mp3"
		else
			-- encountered unknown page
			display dialog unknown_page_msg
			-- correctly exit the loop and end the program
			set found_slot to true
		end if
	end if
	
	-- restore original delimiter before exiting
	set AppleScript's text item delimiters to astid
end repeat